package analizadores;

import java_cup.runtime.*;
import global.Nodo;

parser code 
{:
    public Nodo raiz;

    public void syntax_error(Symbol s){ 
        System.err.println("Error sintáctico en la Línea: " + (s.left + 1) + ", Columna: " + (s.right + 1) + ". No se esperaba este componente: " + s.value + "."); 
    } 

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("Error sintáctico irrecuperable en la Línea: " + (s.left + 1) + ", Columna " + (s.right + 1) + ". Componente " + s.value + " no reconocido.");
    }

:} 

/* Terminales */
terminal String llaa, llac, para, parc, igual, puntoycoma;
terminal String mas, menos, por, dividido, pow;
terminal String igualdad, desigualdad, mayor, menor, mayorigual, menorigual;
terminal String tand, tor, xor, not;
terminal String tboolean, tchar, tint, tdouble, tstring;
terminal String booleano, caracter, cadena, entero, doble, identificador;
terminal String print, repetir;
  
/* No terminales */
non terminal Nodo INICIO, BLOQUE, L_INSTRUCCIONES, INSTRUCCION, DECLARACION, TIPO, E, IMPRIMIR, REPET;
non terminal Nodo CONDICION, RELACION, VALOR;

/* Precedencia */
precedence left tor;
precedence left tand;
precedence left xor;
precedence nonassoc igualdad, desigualdad, menor, menorigual, mayor, mayorigual;
precedence left mas, menos;
precedence left por, dividido;
precedence right para, parc;
precedence nonassoc pow;
precedence right not;

start with INICIO; 

/* Funcionalidad */
INICIO          ::=     BLOQUE:a
                        {: 
                            Nodo padre = new Nodo("S", "");
                            padre.agregarHijo(a);
                            parser.raiz = padre;
                        :};

BLOQUE          ::=     llaa llac
                        {: 
                            RESULT = new Nodo("BLOQUE", "");    
                        :}
                |       llaa L_INSTRUCCIONES:a llac
                        {: 
                            RESULT = new Nodo("BLOQUE", ""); 
                            RESULT.agregarHijo(a);
                        :};

L_INSTRUCCIONES ::=     L_INSTRUCCIONES:a INSTRUCCION:b
                        {: 
                            RESULT = new Nodo("L_INSTRUCCIONES", ""); 
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       INSTRUCCION:a
                        {: 
                            RESULT = new Nodo("L_INSTRUCCIONES", ""); 
                            RESULT.agregarHijo(a);
                        :};

INSTRUCCION     ::=     BLOQUE:a 
                        {: 
                            RESULT = a;
                        :}
                |       DECLARACION:a
                        {: 
                            RESULT = a;
                        :}
                |       IMPRIMIR:a
                        {: 
                            RESULT = a;
                        :}
                |       REPET:a
                        {: 
                            RESULT = a;
                        :};

DECLARACION     ::=     TIPO:a identificador:b igual CONDICION:c puntoycoma
                        {: 
                            RESULT = new Nodo("DECLARACION", "", bleft, bright); 
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(new Nodo("ID", b)); 
                            RESULT.agregarHijo(c);
                        :};

TIPO            ::=     tboolean:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tchar:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tint:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tdouble:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tstring:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :};

CONDICION       ::=     CONDICION:a tand:tkn CONDICION:b  
                        {:  
                            RESULT = new Nodo("&&", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a tor:tkn CONDICION:b
                        {:  
                            RESULT = new Nodo("||", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a xor:tkn CONDICION:b 
                        {:  
                            RESULT = new Nodo("^", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       not:tkn CONDICION:a 
                        {:  
                            RESULT = new Nodo("!", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                        :}
                |       para CONDICION:a parc 
                        {:  
                            RESULT = a;
                        :}
                |       RELACION:a  
                        {:  
                            RESULT = a;
                        :};

RELACION        ::=     CONDICION:a igualdad:tkn CONDICION:c 
                        {:  
                            RESULT = new Nodo("==", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       CONDICION:a desigualdad:tkn CONDICION:c  
                        {:  
                            RESULT = new Nodo("!=", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       CONDICION:a mayor:tkn CONDICION:c  
                        {:  
                            RESULT = new Nodo(">", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       CONDICION:a menor:tkn CONDICION:c 
                        {:  
                            RESULT = new Nodo("<", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       CONDICION:a mayorigual:tkn CONDICION:c  
                        {:  
                            RESULT = new Nodo(">=", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       CONDICION:a menorigual:tkn CONDICION:c
                        {:  
                            RESULT = new Nodo("<=", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       VALOR:a 
                        {:  
                            RESULT = a;
                        :};

VALOR           ::=     CONDICION:a mas:tkn CONDICION:b
                        {:  
                            RESULT = new Nodo("+", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a menos:tkn CONDICION:b 
                        {:  
                            RESULT = new Nodo("-", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a por:tkn CONDICION:b
                        {:  
                            RESULT = new Nodo("*", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a dividido:tkn CONDICION:b  
                        {:  
                            RESULT = new Nodo("/", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a pow:tkn CONDICION:b 
                        {:  
                            RESULT = new Nodo("POW", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a para:tkn CONDICION:b parc  
                        {:  
                            RESULT = new Nodo("*", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       menos:tkn CONDICION:a   
                        {:  
                            RESULT = new Nodo("-", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                        :}
                |       E:a  
                        {:  
                            RESULT = a;
                        :};

E               ::=     booleano:a
                        {: 
                            RESULT = new Nodo("BOOLEANO", a, aleft, aright); 
                        :}
                |       caracter:a
                        {: 
                            RESULT = new Nodo("CARACTER", a, aleft, aright); 
                        :}
                |       entero:a
                        {: 
                            RESULT = new Nodo("ENTERO", a, aleft, aright); 
                        :}
                |       doble:a
                        {: 
                            RESULT = new Nodo("DOBLE", a, aleft, aright); 
                        :}
                |       cadena:a
                        {: 
                            RESULT = new Nodo("CADENA", a.replace("\"",""), aleft, aright); 
                        :}
                |       identificador:a
                        {: 
                            RESULT = new Nodo("ID", a, aleft, aright); 
                        :};

IMPRIMIR        ::=     print para CONDICION:a parc puntoycoma
                        {: 
                            RESULT = new Nodo("IMPRIMIR", ""); 
                            RESULT.agregarHijo(a);
                        :};

REPET           ::=     repetir:rep para CONDICION:a parc BLOQUE:b
                        {: 
                            RESULT = new Nodo("REPETIR", "", repleft, repright); 
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :};
