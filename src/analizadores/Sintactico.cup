package analizadores;

import java_cup.runtime.*;
import global.Nodo;

parser code 
{:
    public Nodo raiz;

    public void syntax_error(Symbol s){ 
        System.err.println("Error sintáctico en la Línea: " + (s.left + 1) + ", Columna: " + (s.right + 1) + ". No se esperaba este componente: " + s.value + "."); 
    } 

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("Error sintáctico irrecuperable en la Línea: " + (s.left + 1) + ", Columna " + (s.right + 1) + ". Componente " + s.value + " no reconocido.");
    }

:} 

/* Terminales */
terminal String llaa, llac, para, parc, igual, puntoycoma;
terminal String mas, menos, por, dividido, pow;
terminal String igualdad, desigualdad, mayor, menor, mayorigual, menorigual;
terminal String tand, tor, xor, not;
terminal String tboolean, tchar, tint, tdouble, tstring;
terminal String booleano, caracter, cadena, entero, doble, identificador;
terminal String print, repetir;
  
/* No terminales */
non terminal Nodo INICIO, BLOQUE, L_INSTRUCCIONES, INSTRUCCION, DECLARACION, TIPO, E, IMPRIMIR, REPET;
non terminal Nodo CONDICION, RELACION, SIGNO_REL, VALOR;

/* Precedencia */
precedence left mas, menos;
precedence left por, dividido;
precedence nonassoc pow;
precedence left para, parc;
precedence left igualdad, desigualdad, menor, menorigual, mayor, mayorigual;
precedence left not;
precedence left xor;
precedence left tor;
precedence left tand;

start with INICIO; 

/* Funcionalidad */
INICIO          ::=     BLOQUE:a
                        {: 
                            Nodo padre = new Nodo("S", "");
                            padre.agregarHijo(a);
                            parser.raiz = padre;
                        :};

BLOQUE          ::=     llaa llac
                        {: 
                            RESULT = new Nodo("BLOQUE", "");    
                        :}
                |       llaa L_INSTRUCCIONES:a llac
                        {: 
                            RESULT = new Nodo("BLOQUE", ""); 
                            RESULT.agregarHijo(a);
                        :};

L_INSTRUCCIONES ::=     L_INSTRUCCIONES:a INSTRUCCION:b
                        {: 
                            RESULT = new Nodo("L_INSTRUCCIONES", ""); 
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       INSTRUCCION:a
                        {: 
                            RESULT = new Nodo("L_INSTRUCCIONES", ""); 
                            RESULT.agregarHijo(a);
                        :};

INSTRUCCION     ::=     BLOQUE:a 
                        {: 
                            RESULT = a;
                        :}
                |       DECLARACION:a
                        {: 
                            RESULT = a;
                        :}
                |       IMPRIMIR:a
                        {: 
                            RESULT = a;
                        :}
                |       REPET:a
                        {: 
                            RESULT = a;
                        :};

DECLARACION     ::=     TIPO:a identificador:b igual CONDICION:c puntoycoma
                        {: 
                            RESULT = new Nodo("DECLARACION", "", bleft, bright); 
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(new Nodo("ID", b)); 
                            RESULT.agregarHijo(c);
                        :};

TIPO            ::=     tboolean:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tchar:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tint:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tdouble:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :}
                |       tstring:a
                        {: 
                            RESULT = new Nodo("TIPO", a);
                        :};

CONDICION       ::=     CONDICION:a tand:tkn CONDICION:b  
                        {:  
                            RESULT = new Nodo("&&", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a tor:tkn CONDICION:b
                        {:  
                            RESULT = new Nodo("||", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       CONDICION:a xor:tkn CONDICION:b 
                        {:  
                            RESULT = new Nodo("^", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       not:tkn para CONDICION:a parc  
                        {:  
                            RESULT = new Nodo("!", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                        :}
                |       RELACION:a  
                        {:  
                            RESULT = a;
                        :};

RELACION        ::=     VALOR:a SIGNO_REL:b VALOR:c  
                        {:  
                            RESULT = b;
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       para VALOR:a SIGNO_REL:b VALOR:c parc  
                        {:  
                            RESULT = b;
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(c);
                        :}
                |       para booleano:a parc 
                        {:  
                            RESULT = new Nodo("BOOLEANO", a, aleft, aright);
                        :}
                |       not:tkn identificador:a 
                        {:  
                            RESULT = new Nodo("!", tkn, tknleft, tknright);
                            RESULT.agregarHijo(new Nodo("ID", a, aleft, aright));
                        :}
                |       not:tkn booleano:a  
                        {:  
                            RESULT = new Nodo("!", tkn, tknleft, tknright);
                            RESULT.agregarHijo(new Nodo("BOOLEANO", a, aleft, aright));
                        :}
                |       para CONDICION:a parc 
                        {:  
                            RESULT = a;
                        :}
                |       VALOR:a 
                        {:  
                            RESULT = a;
                        :};

SIGNO_REL       ::=     igualdad:tkn  
                        {:  
                            RESULT = new Nodo("==", tkn, tknleft, tknright);
                        :}
                |       desigualdad:tkn  
                        {:  
                            RESULT = new Nodo("!=", tkn, tknleft, tknright);
                        :}
                |       mayor:tkn  
                        {:  
                            RESULT = new Nodo(">", tkn, tknleft, tknright);
                        :}
                |       menor:tkn 
                        {:  
                            RESULT = new Nodo("<", tkn, tknleft, tknright);
                        :}
                |       mayorigual:tkn  
                        {:  
                            RESULT = new Nodo(">=", tkn, tknleft, tknright);
                        :}
                |       menorigual:tkn
                        {:  
                            RESULT = new Nodo("<=", tkn, tknleft, tknright);
                        :};

VALOR           ::=     VALOR:a mas:tkn VALOR:b
                        {:  
                            RESULT = new Nodo("+", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       VALOR:a menos:tkn VALOR:b 
                        {:  
                            RESULT = new Nodo("-", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       VALOR:a por:tkn VALOR:b
                        {:  
                            RESULT = new Nodo("*", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       VALOR:a dividido:tkn VALOR:b  
                        {:  
                            RESULT = new Nodo("/", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       VALOR:a pow:tkn VALOR:b 
                        {:  
                            RESULT = new Nodo("POW", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       VALOR:a para:tkn VALOR:b parc  
                        {:  
                            RESULT = new Nodo("*", tkn, tknleft, tknright);
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :}
                |       para VALOR:a parc  
                        {:  
                            RESULT = a;
                        :}
                |       menos:a VALOR:b  
                        {:  
                            RESULT = new Nodo("-", a, aleft, aright);
                            RESULT.agregarHijo(b);
                        :}
                |       E:a  
                        {:  
                            RESULT = a;
                        :};

E               ::=     booleano:a
                        {: 
                            RESULT = new Nodo("BOOLEANO", a, aleft, aright); 
                        :}
                |       caracter:a
                        {: 
                            RESULT = new Nodo("CARACTER", a, aleft, aright); 
                        :}
                |       entero:a
                        {: 
                            RESULT = new Nodo("ENTERO", a, aleft, aright); 
                        :}
                |       doble:a
                        {: 
                            RESULT = new Nodo("DOBLE", a, aleft, aright); 
                        :}
                |       cadena:a
                        {: 
                            RESULT = new Nodo("CADENA", a, aleft, aright); 
                        :}
                |       identificador:a
                        {: 
                            RESULT = new Nodo("ID", a, aleft, aright); 
                        :};

IMPRIMIR        ::=     print para CONDICION:a parc puntoycoma
                        {: 
                            RESULT = new Nodo("IMPRIMIR", ""); 
                            RESULT.agregarHijo(a);
                        :};

REPET           ::=     repetir:rep para CONDICION:a parc BLOQUE:b
                        {: 
                            RESULT = new Nodo("REPETIR", "", repleft, repright); 
                            RESULT.agregarHijo(a);
                            RESULT.agregarHijo(b);
                        :};
